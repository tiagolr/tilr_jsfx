desc:Rippler
author: tilr
version: 1.1.0
changelog:
  Add closed and open tube models
provides:
  rippler.jsfx.RPL
  rippler/logo.png
  rippler/rippler.array.jsfx-inc
  rippler/rippler.envlib.jsfx-inc
  rippler/rippler.gfxlib.jsfx-inc
  rippler/rippler.modal.jsfx-inc
  rippler/rippler.rbj_filter.jsfx-inc
  [data] tilr8_Rippler/mallet01.wav
  [data] tilr8_Rippler/mallet02.wav
  [data] tilr8_Rippler/mallet03.wav
  [data] tilr8_Rippler/mallet04.wav
  [data] tilr8_Rippler/mallet05.wav
  [data] tilr8_Rippler/mallet06.wav
  [data] tilr8_Rippler/mallet07.wav
  [data] tilr8_Rippler/mallet08.wav
  [data] tilr8_Rippler/mallet09.wav
  [data] tilr8_Rippler/mallet10.wav
  [data] tilr8_Rippler/mallet11.wav
  [data] tilr8_Rippler/mallet12.wav
  [data] tilr8_Rippler/mallet13.wav
  [data] tilr8_Rippler/mallet14.wav
  [data] tilr8_Rippler/mallet15.wav
  [data] tilr8_Rippler/mallet16.wav
  [data] tilr8_Rippler/mallet17.wav
  [data] tilr8_Rippler/mallet18.wav
  [data] tilr8_Rippler/mallet19.wav
  [data] tilr8_Rippler/mallet20.wav
  [data] tilr8_Rippler/mallet21.wav
  [data] tilr8_Rippler/mallet22.wav
  [data] tilr8_Rippler/mallet23.wav
  [data] tilr8_Rippler/mallet24.wav
  [data] tilr8_Rippler/mallet25.wav
  [data] tilr8_Rippler/mallet26.wav
  [data] tilr8_Rippler/mallet27.wav
  [data] tilr8_Rippler/mallet28.wav
  [data] tilr8_Rippler/mallet29.wav
  [data] tilr8_Rippler/mallet30.wav
  [data] tilr8_Rippler/mallet31.wav
  [data] tilr8_Rippler/mallet32.wav
  [data] tilr8_Rippler/mallet33.wav
  [data] tilr8_Rippler/mallet34.wav
  [data] tilr8_Rippler/mallet35.wav
  [data] tilr8_Rippler/mallet36.wav
  [data] tilr8_Rippler/mallet37.wav
  [data] tilr8_Rippler/mallet38.wav
  [data] tilr8_Rippler/mallet39.wav
  [data] tilr8_Rippler/mallet40.wav
  [data] tilr8_Rippler/mallet41.wav
  [data] tilr8_Rippler/mallet42.wav
  [data] tilr8_Rippler/mallet43.wav
  [data] tilr8_Rippler/mallet44.wav
  [data] tilr8_Rippler/mallet45.wav
  [data] tilr8_Rippler/mallet46.wav
  [data] tilr8_Rippler/mallet47.wav
  [data] tilr8_Rippler/mallet48.wav
  [data] tilr8_Rippler/mallet49.wav
  [data] tilr8_Rippler/mallet50.wav
screenshot: https://raw.githubusercontent.com/tiagolr/tilr_jsfx/master/doc/rippler.png
about:
  # Rippler

  A physical modeled synth

  **Features:**
  * 7 Models of acoustic resonators: String, Beam, Metal, Membrane, Drumhead, Plate and Marimba.
  * Inharmocity, Tone, Ratio and Material sliders to shape the timbre.
  * Sample based mallet with 50 samples to chose from.
  * Noise generator with filter and ADSR.
  * Up to 64 partials (configurable).
  * Up to 16 polyphony (configurable).
tags: synth, instrument

<? npartials < 1 ? npartials = 32 ?>
<? npolyphony < 1 ? npolyphony = 8 ?>
config: npartials "Partials" 32 4 16 32 64
config: npolyphony "Polyphony" 8 4 8 12 16

options:gfx_hz=60

import rippler.array.jsfx-inc
import rippler.modal.jsfx-inc
import rippler.rbj_filter.jsfx-inc
import rippler.envlib.jsfx-inc
import rippler.gfxlib.jsfx-inc

filename:0,rippler/logo.png

slider1:/tilr8_Rippler:none:Mallet
slider2:click_mix=0<0,1,.0001>-Mallet Mix
slider3:click_res=.8<0,1,.0001>-Mallet Resonance
slider4:click_pitch=0<-24,24,1>-Pitch
slider5:click_ktrack=0<0,1,1{No, Yes}>-KeyTrack
slider6:click_filter_mode=2<0,2,1{LP,BP,HP}>-Mallet Filter Mode
slider7:click_filter_freq=20<20,20000,1:log>-Mallet Filter Freq

slider10:model=0<0,6,1{String,Beam,Squared,Membrane,Plate,Drumhead,Marimba}>-Model
slider11:decay=1<0.01,100,.01:log>-Decay
slider12:freq_damp=0<-1,1,.001>-Material
slider13:tone=0<-1,1,0.1>-Tone
slider14:hit_position=26<2,50,0.1>-Hit Position
slider15:release=1<0,1,.001>-Release
slider16:inharmonic=0.0001<0.0001,1,.001:log>-Inharmonic
slider17:ratio=1<0.1,10,0.01:log>-Ratio
slider18:lowcut=20<20,20000,1:log>-Low Cut

slider20:noise_mix=0<0,1,.0001>-Noise Mix
slider21:noise_res=0<0,1,.0001>-Noise Res
slider22:noise_filter_mode=2<0,2,1{LP,BP,HP}>-Noise Filter Mode
slider23:noise_filter_freq=20<20,20000,1:log>-Noise Filter Freq
slider24:noise_filter_q=.707<0.707,4,.01:log>-Noise Filter Q
slider25:noise_att=1<1, 5000, 1:log>-Noise Attack
slider26:noise_dec=500<1, 5000, 1:log>-Noise Decay
slider27:noise_sus=0<0, 100, 0.1>-Noise Sustain
slider28:noise_rel=500<1, 5000, 1:log>-Noise Release
slider29:_tube_radius=50<0,100,0.01>-Tube radius

slider40:vel_click_mix=0<0,1,0.01>-Vel Click Mix
slider41:vel_click_res=0<0,1,0.01>-Vel Click Res
slider43:vel_noise_mix=0<0,1,0.01>-Vel Noise Mix
slider44:vel_noise_res=0<0,1,0.01>-Vel Noise Res
slider46:vel_res_decay=0<0,1,0.01>-Vel Decay
slider47:vel_res_hit=0<0,1,0.01>-Vel Hit
slider48:vel_res_inharm=0<0,1,0.01>-Vel Inharm

@init
ext_noinit = 1;
<? printf("npartials = %d;\n", npartials); ?>
<? printf("npolyphony = %d;\n", npolyphony); ?>
lfile = -1;
lmodel = model;
wavechn = 0;
wavesrate = 0;
wavelen = 0;
base_note = 60;
playback_speed = 1;
nstring = 0;
vel_mapping = 0;
freemem = 100000;
freemem = init_models(freemem);

models = freemem; freemem += 9;
models[0] = string;
models[1] = beam;
models[2] = metal;
models[3] = membrane;
models[4] = plate;
models[5] = drumhead;
models[6] = marimba;
models[7] = 0; // open tube uses no modals
models[8] = 0; // closed tube uses no modals

freemem = poly.array_init(freemem, 128, 7); // [note, velocity, freq, sample_pos, playback_speed, nstring, release]
freemem = midi.array_init(freemem, 256, 4); // [offset, status, note, vel]
wavebuf = freemem; freemem += 500000; // mallet sample
combbuf = freemem; // comb used for stereo effect
combpos = 0;
combsize = (20 * srate / 1000)|0;
freemem += combsize;
tube_buf = freemem;
tube_len = 20000; // buffer size, 20000 allows for 10Hz at 200k srate (max_size = srate / freq_min)
freemem += tube_len * npolyphony; // allocate npolyphony buffers for tube waveguides

function db2gain (db) local (val) (
  val = 10^(db / 20);
  val <= 0.001 ? 0 : val;
);

function normalize_vol_slider(val) ( val * 60 / 100 - 60 );
function note2freq(n) ( 440 * pow(2, (n - 69) / 12); );

function read_file(filehandle)
local(i)
(
  filehandle > 0 ? (
    file_riff(filehandle, wavechn, wavesrate);
    wavechn ? (
      wavelen = file_avail(filehandle);
      file_mem(filehandle,wavebuf, min(wavelen, 500000));
    );
    file_close(filehandle);
    wavechn == 2 ? (
      wavelen = (wavelen / 2) | 0;
      i = 0; loop(wavelen,
        wavelen[i] = wavelen[2*i] + wavelen[2*i+1]; // stereo to mono
        i += 1;
      );
    );
  );
);

function on_file_change() (
  lfile = slider1;
  fhandle = file_open(slider1);
  read_file(fhandle);
);

function on_slider()
(
  lmodel != model ? (
    ratio = model == 1 ? 2 : 0.78;
    <?
      loop(i=1;npolyphony,
        printf("s%02d.string_clear(%d);\n", i, i);
        printf("noise_env%02d.env_reset();\n", i);
        i+=1;
      );
    ?>
  );
  lmodel = model;
  model == 1 ? recalc_beam();
  model == 3 ? recalc_membrane();
  model == 4 ? recalc_plate();

  tube_radius = 0.01 + _tube_radius * 0.89 / 100; // map tube radius [0,100] to [0.01, 0.9]
  lfile != slider1 || fhandle == -1 ? on_file_change();
  playback_speed = 2^((click_pitch / 12) | 0);
  lowcut_filter.rbj_hp(lowcut, 0.707);
  combsize = (20 * srate / 1000)|0;
  click_filter_mode == 0 ? (
    <?
      loop(i=1;npolyphony,
        printf("click_filter%02d.rbj_lp(click_filter_freq, 0.707);\n",i);
        i+=1;
      );
    ?>
  );
  click_filter_mode == 1 ? (
    <?
      loop(i=1;npolyphony,
        printf("click_filter%02d.rbj_bp(click_filter_freq, 0.707);\n",i);
        i+=1;
      );
    ?>
  );
  click_filter_mode == 2 ? (
    <?
      loop(i=1;npolyphony,
        printf("click_filter%02d.rbj_hp(click_filter_freq, 0.707);\n",i);
        i+=1;
      );
    ?>
  );
  noise_filter_mode == 0 ? (
    <?
      loop(i=1;npolyphony,
        printf("noise_gen%02d.rbj_lp(noise_filter_freq, noise_filter_q);\n",i);
        i+=1;
      );
    ?>
  );
  noise_filter_mode == 1 ? (
    <?
      loop(i=1;npolyphony,
        printf("noise_gen%02d.rbj_bp(noise_filter_freq, noise_filter_q);\n",i);
        i+=1;
      );
    ?>
  );
  noise_filter_mode == 2 ? (
    <?
      loop(i=1;npolyphony,
        printf("noise_gen%02d.rbj_hp(noise_filter_freq, noise_filter_q);\n",i);
        i+=1;
      );
    ?>
  );
  <?
    loop(i=1;npolyphony,
      printf("s%02d.string_init(s%02d.f0, s%02d.rel);\n", i, i, i);
      printf("noise_env%02d.env_adsr(noise_att, noise_dec, normalize_vol_slider(noise_sus), noise_rel, 0.4, 0.4, 0.4);\n",i);
      i += 1;
    );
  ?>
);

function process_noise()
local (noise)
(
  noise = rand(2) - 1;
  noise_filter_mode == 1 ||
  (noise_filter_mode == 0 && noise_filter_freq < 20000) ||
  (noise_filter_mode == 2 && noise_filter_freq > 20) ?
  (
    noise = this.rbj_df1(noise);
  );
  noise
);

on_file_change(); // fix force first file open

@slider

on_slider();

@block

// remove queued midi messages that have been processed
ptr = midi.array_first();
while (ptr >= 0) (
  ptr[0] < 0 ? (
    midi.array_remove(ptr);
    ptr = midi.array_first();
  ) : (
    ptr = midi.array_next(ptr);
  );
);

while (midirecv(offset, msg1, note, vel)) (
  event = msg1 & 0xF0;
  channel = msg1 & 0x0F;

  // note on/off
  event == 0x90 || event == 0x80 ? (
    // prevent midi buffer overflow
    midi.size == 256 ? (
      midi.array_remove(midi.array_first());
    );
    ptr = midi.array_add();
    ptr[0] = offset;
    ptr[1] = msg1;
    ptr[2] = note;
    ptr[3] = vel;
  );

  // All notes off
  event == 0xB0 && note == 123 ? (
    poly.array_clear();
  );

  midisend(offset, msg1, note, vel);
);

@sample
function on_note(p)
local (ptr, note_repeat, note, vel, env, freq)
(
  note = p[2];
  vel = p[3];

  // remove note if it is already playing
  ptr = poly.array_find(note);
  ptr >= 0 ? poly.array_remove(ptr);

  freq = note2freq(note);
  ptr = poly.array_add();
  ptr[0] = note;
  ptr[1] = vel / 127;
  ptr[2] = freq / srate;
  ptr[3] = 0; // sample pos
  ptr[4] = 2 ^ ((note - base_note) / 12); // playback speed
  ptr[5] = nstring;
  ptr[6] = 0; // note release

  <?
    loop(i=1;npolyphony,
      printf("nstring == %d ? (\n", i-1);
      printf("  s%02d.vel = ptr[1];\n", i);
      printf("  s%02d.string_init(freq, 0);\n", i);
      printf("  s%02d.active = 1; s%02d.silence = 0;\n", i, i);
      printf("  noise_env%02d.env_a(1);\n", i);
      printf("  noise_env%02d.vel = ptr[1];\n", i);
      printf(");\n");
      i += 1;
    );
  ?>

  nstring = (nstring + 1) % npolyphony;
);

function off_note(p)
local(ptr, note, pp, freq)
(
  note = p[2];
  ptr = poly.array_find(note);
  ptr >= 0 ? (
    <?
      loop(i=1;npolyphony,
        printf("ptr[5] == %d ? s%02d.string_init(s%02d.f0, 1);\n", i-1, i, i);
        printf("ptr[5] == %d ? noise_env%02d.env_r();\n", i-1, i);
        i += 1;
      );
    ?>
    ptr[6] = 1;
  );
);

function handle_midi(p)
local(msg1, vel, event)
(
  msg1 = p[1];
  vel = p[3];
  event = msg1 & 0xF0;
  event == 0x90 && vel ? on_note(p); // Note on
  event == 0x80 || (event == 0x90 && !vel) ? off_note(p); // Note off
);

/**
  Sample linear interpolation
*/
function wave_lerp(buf, pos)
local(x, i, j)
(
  i = pos|0;
  x = pos - i;
  j = i + 1;
  j >= wavelen ? j = 0;
  (1 - x) * buf[i] + x * buf[j];
);

// process queued midi
ptr = midi.array_first();
while(ptr >= 0) (
  ptr[0] == 0 ? ( // offset has reached zero
    handle_midi(ptr);
  );
  ptr[0] -= 1;
  ptr = midi.array_next(ptr);
);

// process notes being played
ptr = poly.array_first();
<?
  loop(i=1;npolyphony,
    printf("outm_%02d = 0;\n",i); // out mallet into resonance
    i+=1;
  );
?>
mallet = 0; // total mallet output
while(ptr >= 0) ( // for each note/voice
  ptr[3] >= 0 ? ( // if note is playing sample
    out = wave_lerp(wavebuf, ptr[3]);

    click_filter_mode == 1 ||
    (click_filter_mode == 2 && click_filter_freq > 20) ||
    (click_filter_mode == 0 && click_filter_freq < 20000) ?
    (
      <?
        loop(i=1;npolyphony,
          printf("ptr[5] == %d ? out = click_filter%02d.rbj_df1(out);\n", i - 1, i);
          i+=1;
        );
      ?>
    );

    mallet += out * min(1, click_mix + vel_click_mix * ptr[1]);
    ptr[3] += click_ktrack // samplepos += playback_speed
      ? ptr[4] * playback_speed
      : playback_speed;
    ptr[3] >= wavelen ? ptr[3] = -1; // finished sample playback
    <?
      loop(i=1;npolyphony,
        printf("ptr[5] == %d ? outm_%02d = out * min(1, click_res + vel_click_res * ptr[1] );\n",i-1, i);
        i+=1;
      );
    ?>
  );
  ptr = poly.array_next(ptr);
);

out_res = 0; // out resonance
out_noise = 0;
splmono = (spl1 + spl2) / 2;
<?
  loop(i=1;npolyphony,
    // process noise
    printf("noise_env%02d.state > 0 ? (\n", i);
    printf("  noise_env%02d.env_process();\n", i);
    printf("  n = noise_gen%02d.process_noise();\n", i);
    printf("  n *= noise_env%02d.env;\n", i);
    printf("  out_noise += n * min(1, noise_mix + vel_noise_mix * noise_env%02d.vel);\n", i);
    printf("  outm_%02d += n * min(1, noise_res + vel_noise_res * noise_env%02d.vel);\n", i, i);
    printf(");\n");
    // process resonance
    printf("out_res += s%02d.string_process(splmono + outm_%02d, %d);\n", i, i, i);
    i += 1;
  );
?>

lowcut > 20 ? (
  out_res = lowcut_filter.rbj_df1(out_res);
);

output = mallet + out_res + out_noise;
combbuf[combpos] = output; // comb stereo FX similar to Haas
combpos = (combpos + 1) % combsize;
spl0 = output + combbuf[combpos] * 0.33;
spl1 = output - combbuf[combpos] * 0.33;

// remove notes that finish playing and are no longer pressed
ptr = poly.array_first();
while (ptr >= 0) (
  ptr[3] == -1 && ptr[6] ? (
    poly.array_remove(ptr);
    ptr = poly.array_first();
  ) : (
    ptr = poly.array_next(ptr);
  );
);

@gfx 600 250
gfx_clear = COLOR_BG;
mouse.update_mouse_state();

gfx_a = 0.9;
gfx_x = 300; gfx_y = 130;
gfx_blit(0,1,0); // draw logo
gfx_a = 1;
show_help && mouse.left_click
  ? show_help = 0
  : mouse.left_click && mouse_in_rect(300,130,90,60) ?
(
  show_help = 1;
);

set_color(0x666666);
gfx_x = 20; gfx_y = 20;
gfx_drawstr("NOISE");

filter_name = noise_filter_mode == 0 ? "LP" : noise_filter_mode == 1 ? "BP" : "HP";
draw_button(140,20,40,filter_name, 0);
mouse.left_click && mouse_in_rect(140, 20 - 2, 40, 10 + 2) ? (
  gfx_x = 140; gfx_y = 30;
  choice = gfx_showmenu("LP|BP|HP");
  choice > 0 ? slider(22) = choice - 1;
  on_slider();
);

draw_knob(20, 40, 20, "Mix", 0, 0, 1, 0, 0, sprintf(#, "%i%%", slider(20) * 100), 43);
draw_knob(80, 40, 21, "Res", 0, 0, 1, 0, 0, sprintf(#, "%i%%", slider(21) * 100), 44);
draw_knob(140, 40, 23, "Filter", 20, 20, 20000, 1, 0, sprintf(#, "%i Hz", slider(23)), 0);

draw_knob(20, 110, 25, "A", 1, 1, 5000, 1, 0, sprintf(#, "%i ms", slider(25)), 0);
draw_knob(80, 110, 26, "D", 500, 1, 5000, 1, 0, sprintf(#, "%i ms", slider(26)), 0);
draw_knob(140, 110, 24, "Q", .707,.707,4, 0,0, sprintf(#, "%.1f", slider(24)), 0);

draw_knob(20, 180, 27, "S", 0, 0, 100, 0, 0, sprintf(#, "%i%%", slider(27)), 0);
draw_knob(80, 180, 28, "R", 500, 1, 5000, 1, 0, sprintf(#, "%i ms", slider(28)), 0);

set_color(0x666666);
gfx_line(200, 20, 200, 230);
gfx_x = 220; gfx_y = 20;
gfx_drawstr("MALLET");

filter_name = click_filter_mode == 0 ? "LP" : click_filter_mode == 1 ? "BP" : "HP";
draw_button(340,20,40,filter_name, 0);
mouse.left_click && mouse_in_rect(340, 20 - 2, 40, 10 + 2) ? (
  gfx_x = 340; gfx_y = 30;
  choice = gfx_showmenu("LP|BP|HP");
  choice > 0 ? slider(6) = choice - 1;
  on_slider();
);

draw_button(210,180,60,"KTrack", click_ktrack);
mouse.left_click && mouse_in_rect(210,180-2, 60, 10+2) ? (
  click_ktrack = !click_ktrack;
);
draw_button_vel(210,220,60,"VelMap", vel_mapping);
mouse.left_click && mouse_in_rect(210,220-2, 60, 10+2) ? (
  vel_mapping = !vel_mapping;
);

draw_knob(220, 40, 2, "Mix", 0, 0, 1, 0, 0, sprintf(#, "%i%%", slider(2) * 100), 40);
draw_knob(280, 40, 3, "Res", .8, 0, 1, 0, 0, sprintf(#, "%i%%", slider(3) * 100), 41);
draw_knob(340, 40, 7, "Filter", 20, 20, 20000, 1, 0, sprintf(#, "%i Hz", slider(7)), 0);
draw_knob(220, 110, 4, "Pitch", 0, -24, 24, 0, 1, sprintf(#, "%i", slider(4)), 0);

set_color(0x666666);
gfx_line(400, 20, 400, 230);
gfx_x = 420; gfx_y = 20;
gfx_drawstr("RES");

model_name = model == 0 ? "String"
  : model == 1 ? "Beam"
  : model == 2 ? "Squared"
  : model == 3 ? "Membrane"
  : model == 4 ? "Plate"
  : model == 5 ? "Drumhead"
  : model == 6 ? "Marimba"
  : model == 7 ? "Open Tube"
  : "Closed Tube";

draw_button(480,20,100,model_name,1);
mouse.left_click && mouse_in_rect(480, 20 - 2, 100, 10 + 2) ? (
  gfx_x = 500; gfx_y = 30;
  choice = gfx_showmenu("String|Beam|Squared|Membrane|Plate|Drumhead|Marimba|Open Tube|Closed Tube");
  choice > 0 ? slider(10) = choice - 1;
  on_slider();
);

draw_knob(420, 40, 11, "Decay", 1, 0.01, 100, 1, 0, sprintf(#, "%.2f s", slider(11)), 46);
draw_knob(540, 40, 15, "Relse", 1, 0, 1, 0, 0, sprintf(#, "%i%%", slider(15) * 100), 0);

model >= 7 ? (
  draw_knob(480, 40, 29, "Radius", 50, 0, 100, 0, 0, sprintf(#, "%i%%", slider(29)), 0);
);

model < 7 ? (
  draw_knob(480, 40, 12, "Matrl", 0, -1, 1, 0, 1, sprintf(#, "%i%%", slider(12) * 100), 0);
  draw_knob(420, 110, 13, "Tone", 0, -1, 1, 0, 1, sprintf(#, "%i%%", slider(13) * 100), 0);
  draw_knob(480, 110, 14, "Hit", 26, 2, 50, 0, 0, sprintf(#, "%i%%", slider(14)), 47);
  draw_knob(540, 110, 16, "Inharm", 0.0001, 0.0001, 1, 1,0, sprintf(#, "%.2f", slider(16) * 100), 48);
  model == 1 || model == 3 || model == 4 ? (
    draw_knob(420, 180, 17, "Ratio", 1, 0.1, 10, 1, 0, sprintf(#, "%.1f", slider(17)), 0);
  );
);

draw_knob(540, 180, 18, "LowCut", 20, 20, 20000, 1, 0, sprintf(#, "%i Hz", slider(18)), 0);

function on_knob_move (nslider, slider_min, slider_max, slider_is_log, factor) (
  factor *= mouse.control ? 0.05 : 1;
  slider_is_log ? (
    slider(nslider) = slider(nslider) * pow(100, -factor * 0.01);
  ) : (
    inc = (slider_max - slider_min) / 100 * -factor;
    slider(nslider) += inc;
  );

  slider(nslider) > slider_max ? slider(nslider) = slider_max;
  slider(nslider) < slider_min ? slider(nslider) = slider_min;
  on_slider();
  sliderchange(slider(nslider));
  slider_automate(slider(nslider));
);

function on_velknob_move(nslider, factor) (
  factor *= mouse.control ? 0.05 : 1;
  slider(nslider) = max(0,min(1,slider(nslider) + 0.01 * -factor));
  sliderchange(slider(nslider));
  slider_automate(slider(nslider));
);

wheelknob_nslider ? (
  on_knob_move(wheelknob_nslider, wheelknob_min, wheelknob_max, wheelknob_is_log, mouse.wheel * -7);
);

selknob_nslider && mouse.left && mouse.dy != 0 ? (
  on_knob_move(selknob_nslider, selknob_min, selknob_max, selknob_is_log, mouse.dy);
);

selknob_velslider && mouse.left && mouse.dy ? (
  on_velknob_move(selknob_velslider, mouse.dy);
);

doubleclk_nslider ? (
  on_slider();
  sliderchange(slider(doubleclk_nslider));
  slider_automate(slider(doubleclk_nslider));
  doubleclk_nslider = 0;
);

gfx_setcursor(selknob_nslider ? 32511 : 0);
!mouse.wheel ? (
  wheelknob_nslider = 0;
  wheelknob_velslider = 0;
);
!mouse.left ? (
  selknob_nslider = 0;
  selknob_velslider = 0;
);

function draw_help ()
local(xsize, ysize, txt, pad)
(
  gfx_setfont(1, "Arial", 16);
  xsize = 400;
  ysize = 210;
  pad = 20;
  gfx_x = 0; gfx_y = 0;
  gfx_blurto(gfx_w, gfx_h);
  gfx_x = 0; gfx_y = 0;
  gfx_blurto(gfx_w, gfx_h);
  gfx_set(0,0,0,0.8);
  gfx_rect(gfx_w/2 - xsize/2, gfx_h/2 - ysize/2, xsize, ysize);
  txt = #;
  strcpy(txt, "Rippler 1.1.0\nTilr 2025\n\n");
  strcat(txt, "- Change polyphony and partials in window menu.\n");
  strcat(txt, "- Ctrl for fine slider adjustments.\n");
  strcat(txt, "- Alt to adjust velocity mapping.\n");
  strcat(txt, "- Double click to reset sliders.\n");
  strcat(txt, "- Rippler supports audio in, press notes to send\nthe signal through to the ressonators.\n");
  gfx_x = gfx_w/2 - xsize/2 + pad;
  gfx_y = gfx_h/2 - ysize/2 + pad;
  gfx_set(1,1,1);
  gfx_drawstr(txt,0,gfx_w/2+xsize/2-pad,gfx_h/2+ysize/2-pad);
  gfx_setfont(0);
);

show_help ? draw_help()








