desc:ws.gfxlib.jsfx-inc

@init

COLOR_ACTIVE = 0x11bbff;
COLOR_BG = 0x141618;

selknob_nslider = 0;
selknob_min = 0;
selknob_max = 0;
selknob_is_log = 0;

wheelknob_nslider = 0;
wheelknob_min = 0;
wheelknob_max = 0;
wheelknob_is_log = 0;

doubleclk_nslider = 0;

function deg2rad (deg) (deg * $pi / 180;);
RAD130 = deg2rad(130);

function update_mouse_state()
instance(cap, x, y, lx, ly, dx, dy, right_click, left_click, lleft, lright, left, right, click_time, double_click, control, lwheel, wheel)
global(mouse_cap, mouse_x, mouse_y, mouse_wheel)
(
  lleft = left;
  lright = right;
  lx = x;
  ly = y;
  cap = mouse_cap;
  control = mouse_cap & 4;
  x = mouse_x;
  y = mouse_y;

  left = cap & 1 > 0;
  right = cap & 2 > 0;
  left_click = left && lleft == 0;
  right_click = right && lright == 0;
  dx = x - lx;
  dy = y - ly;

  wheel = mouse_wheel > lwheel ? 1 : mouse_wheel < lwheel ? -1 : 0;
  lwheel = mouse_wheel;

  left_click ? (
    time_precise() - click_time < .5 ? double_click = 1;
    click_time = time_precise();
  ) : (
    double_click = 0;
  );
);

function set_color(color) (
  gfx_r = (color & 0xFF0000) / 0xFF0000;
  gfx_g = (color & 0x00FF00) / 0x00FF00;
  gfx_b = (color & 0x0000FF) / 0x0000FF;
);

function draw_wave(x, y, w, h, buf, len)
local(i, _x, _y)
(
  set_color(COLOR_ACTIVE);
  loop(i = 0; len,
    _x = i * w / len + x;
    _y = buf[i] * h / 2 + h / 2 + y;
    i == 0 ? (
      gfx_x = _x;
      gfx_y = _y;
    );
    gfx_lineto(_x, _y);
    i += 1;
  );
);

function log_scale (value, max, min)
local (minP, maxP, scale) (
    minP = min;
    maxP = max;

    minV = log(min);
    maxV = log(max);

    scale = (maxV - minV) / (maxP - minP);
    exp(minV + scale * (value - minP));
);

function inverse_log_scale (lg, max, min)
local (minP, maxP, scale) (
    minP = min;
    maxP = max;

    minV = log(min);
    maxV = log(max);

    scale = (maxV - minV) / (maxP - minP);
    (log(lg) - minV) / scale + minP;
);

function mouse_in_rect (x, y, w ,h) (
  mouse.x >= x && mouse.x <= x + w && mouse.y >= y && mouse.y <= y + h;
);

function draw_line_thick(x, y, x2, y2, width) 
local (a,r)
(
  r = width/2;
  gfx_circle(x,y,r,1);
  gfx_circle(x2,y2,r,1);
  a = atan2(y2-y, x2-x) + $pi/2;
  gfx_triangle(x+r*cos(a), y+r*sin(a), x-r*cos(a), y-r*sin(a), x2-r*cos(a), y2-r*sin(a), x2+r*cos(a), y2+r*sin(a));
);

function draw_knob(x, y, nslider, label, default, _min, _max, is_log, is_sym, val_label, snap)
local (scale)
(
  set_color(0x282D32);
  gfx_arc(x+20, y+20, 20, -RAD130, RAD130, 1);
  gfx_arc(x+20, y+20, 19.5, -RAD130, RAD130, 1);
  gfx_arc(x+20, y+20, 19, -RAD130, RAD130, 1);
  gfx_arc(x+20, y+20, 18.5, -RAD130, RAD130, 1);

  slider_val = slider(nslider);
  snap ? slider_val = slider(nslider) | 0;
  nslider == 15 && lslider != slider_val ? (
    lsliderrrr += 1;
    lsliderr = slider_val;
    lsliderrr = lslider;
    lslider = slider_val;
  );
  is_log ? (
    slider_val = inverse_log_scale(slider_val, _max, _min);
  );

  // linear map value from min/max to -130deg +130deg
  scale = (130 - -130) / (_max-_min);
  _offset = (-_min * (130 - -130)) / (_max - _min) + -130;
  slider_deg = slider_val * scale + _offset;
  slider_rad = deg2rad(slider_deg);

  gfx_circle(x+20, y+20, 15, 1, 1);
  set_color(COLOR_ACTIVE);
  gfx_circle(x+20-sin(-slider_rad)*10, y+20-cos(-slider_rad)*10, 3, 1);

  gfx_arc(x+20, y+20, 20, is_sym ? 0 : -RAD130, slider_rad, 1);
  gfx_arc(x+20, y+20, 19.5, is_sym ? 0 : -RAD130, slider_rad, 1);
  gfx_arc(x+20, y+20, 19, is_sym ? 0 : -RAD130, slider_rad, 1);
  gfx_arc(x+20, y+20, 18.5, is_sym ? 0 : -RAD130, slider_rad, 1);

  set_color(0xFFFFFF);
  gfx_x = x - 20;
  gfx_y = y + 20 * 2 + 5;
  selknob_nslider == nslider ? (
    gfx_drawstr(val_label, 1, x+20+20*2, y+100);
  ) : (
    gfx_drawstr(label, 1, x+20+20*2, y+100);
  );

  mouse_in_rect(x, y, 40, 40) ? (
    mouse.double_click ? (
      slider(nslider) = default;
      doubleclk_nslider = nslider;
    );
    mouse.left_click ? (
      selknob_nslider = nslider;
      selknob_min = _min;
      selknob_max = _max;
      selknob_is_log = is_log;
    );
    mouse.wheel ? (
      wheelknob_nslider = nslider;
      wheelknob_min = _min;
      wheelknob_max = _max;
      wheelknob_is_log = is_log;
    );
  );
);

function draw_button (x, y, w, label, toggled) (
  set_color(COLOR_ACTIVE);
  gfx_rect(x, y - 1, w, 17 + 2, toggled);
  set_color(toggled ? COLOR_BG : COLOR_ACTIVE);
  gfx_x = x; gfx_y = y - 1;
  gfx_drawstr(label, 5, x+w, y+17);
);
