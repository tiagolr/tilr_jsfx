desc: Vel to Slider
author: tilr
version: 2.0.0
changelog:
  * Added
    - slider for Inverted Value
  * Changed
    - Changed name from "Vel to CC" (effect never output MIDI CC)
about:
  # Vel to Slider

  Maps velocity of incoming MIDI note-on messages to automated slider positions,
  which can be used by REAPER to link to other effects' parameter automation.

  Key features:
  - Provides automated "Value" and "Inverted Value" sliders
  - _(optional)_ Filter by MIDI note/range
  - _(optional)_ Clamp slider value range

in_pin:none
out_pin:none

slider1:0<0, 127, 1>Note min
slider2:127<0, 127, 1>Note max
slider3:0<0, 127, 1>Param min
slider4:127<0, 127, 1>Param max
slider5:0<0, 1, 1{No,Yes}>Ignore zero vel
slider7:0<0, 127, 1>Value
slider8:0<0, 127, 1>Inverted Value

////////////////////////////////////////////////////////////////////////////////
@init
statNoteOn = $x90;
statNoteOff = $x80;

////////////////////////////////////////////////////////////////////////////////
@slider
slider1 > slider2 ? slider2 = slider1;
slider3 > slider4 ? slider3 = slider4;
noteMin = slider1;
noteMax = slider2;
paramMin = slider3;
paramMax = slider4;
ignoreZeroVel = slider5;

////////////////////////////////////////////////////////////////////////////////
@block

while
(

  midirecv(offset,msg1,note,vel) ?
  (

    // Extract message type and channel
    status = msg1 & $xF0;
    channel = msg1 & $x0F;

    // Is it a note event?
    status == statNoteOn && (!ignoreZeroVel || vel) ?
    (
        note <= noteMax && note >= noteMin ? (
          // linear mapping
          scale = (paramMax-paramMin) / 127;
          value = ((vel * scale) + paramMin) | 0;
          slider7 = value;
          slider8 = (paramMin + (paramMax - value)) | 0;
        );
    );

    midisend(offset,msg1,note,vel); // passthrough
    1; // Force loop to continue until all messages have been processed
  );

);
